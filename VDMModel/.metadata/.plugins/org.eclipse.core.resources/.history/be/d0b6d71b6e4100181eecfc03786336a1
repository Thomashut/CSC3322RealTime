class Controller

instance variables

-- sensor objects
sensor1: IRSensor;
sensor2: IRSensor;
sensor3: IRSensor;
sensor4: IRSensor;
sensor5: IRSensor;

-- servo objects
leftServo: Servo;
rightServo: Servo;

-- Helper Flags
started: bool;
stopped: bool;

-- Measurement Object
measureObject: Measurement;

operations

-- constructor for Controller
public Controller: IRSensor * IRSensor * IRSensor * IRSensor * IRSensor * Servo * Servo * Measurement ==> Controller
Controller(lf1, lf2, lf3, lf4, lf5, ls, rs, m) == (
	sensor1 := lf1;
	sensor2 := lf2;
	sensor3 := lf3;
	sensor4 := lf4;
	sensor5 := lf5;
	leftServo := ls;
	rightServo := rs;
	measureObject := m;
);

-- main loop
Step: () ==> ()
Step() == cycles(20) (
		-- Initially the robot attempts to find the line by simply diving forward.
		while (sensor1.getReading() >= irThreashold and sensor2.getReading() >= irThreashold and (not started)) do (
			IO`printf("%s \n","Hunting for Line"); 
			leftServo.setSpeed(0.8);
			rightServo.setSpeed(0.8)
		);
		
		-- Robot has finally found the line. Start Measuring it's length
		if(sensor1.getReading() < irThreashold and sensor2.getReading() < irThreashold and (not started)) then (
			started := true;
			measureObject.startReading();
		);
		
		-- If both sensors detect the black line then robot must be on top of the line
		if(sensor1.getReading() < irThreashold and sensor2.getReading() < irThreashold) then (
			IO`printf("%s \n","Following Line");
			leftServo.setSpeed(0.8);
			rightServo.setSpeed(0.8)
		);
		
		-- Left Sensor detects blackline, right detects white. Turn left to correct
		if(sensor1.getReading() < irThreashold and sensor2.getReading() >= irThreashold) then (
			IO`printf("%s \n","Turning Left");
			/*
				Completely stop the robot first. This may be slower to navigate a black line but it should be more accurate.
				This is because there will be minimal adjustments and overdrive when the sensors return different readings.
				It is slower however as the robot will be stopping and starting every time the enviroment changes.
			*/
			leftServo.setSpeed(0.0);
			rightServo.setSpeed(0.0);
			
			rightServo.setSpeed(0.8);
		); 
		
		-- Left Sensor detects white and right detects black. Turn right to correct
		if(sensor1.getReading() >= irThreashold and sensor2.getReading() < irThreashold) then (
			IO`printf("%s \n","Turning Left");
			leftServo.setSpeed(0.0);
			rightServo.setSpeed(0.0);
			
			leftServo.setSpeed(0.8);
		);
		
		-- Robot has either completly lost the line or it has ended. Either way terminate and stop measuring.
		if(sensor1.getReading() >= irThreashold and sensor2.getReading() >= irThreashold and started) then (
			measureObject.stopReading();
			leftServo.setSpeed(0.0);
			rightServo.setSpeed(0.0);
			
		);


);

-- debug information
PrintReadings: () ==> ()
PrintReadings() == (
    IO`printf("Readings: [%s, %s, %s, %s, %s]\n", 
      [sensor1.getReading(), 
       sensor2.getReading(), 
       sensor3.getReading(),
       sensor4.getReading(),
			 sensor5.getReading()]);
)

thread
periodic(10E6, 0, 0, 0)(Step)


values
	irThreashold = 150; -- Used to detect if the robot has found the black line or not. >= 150 = white, < 150 = black

end Controller